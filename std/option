let typing = use "std/typing";
let panicking = use "std/panicking";

func Option(TData) {

    let _OptionT = type {
        field _data = TData;
        field _containsValue = typing::Bool;

        func some(value) {
            let option = _OptionT {};
            option._data = value;
            option._containsValue = true;
            return option;
        }

        func none() {
            let option = _OptionT {};
            option._containsValue = false;
            return option;
        }

        method hasValue(self) {
            return self._containsValue;
        }

        method unwrap(self) {
            if (self._containsValue) {
                return self._data;
            }
            panicking::panic("attempted to perform 'unwrap' on an Option::none()")
        }

        method unwrapOr(self, default) {
            if (self._containsValue) {
                return self._data;
            }
            return default;
        }
    };

    return _OptionT;
}